#!/usr/bin/env zsh
source ${0:A:h}/../../deploy
source config/.zshenv

ZDOTDIR="$XDG_CONFIG_HOME/zsh"
if [[ $(_os) == macos ]]; then
 # I opted for `.zprofile` over the commonly suggested `.zshenv` due to macOS's quirks. Turns out, `/usr/libexec/path_helper` reorders the `$PATH` between loading `.zshenv` and `.zprofile`. What a great "helper", right?

 # This great article explains what `path_helper` does in more detail: https://gist.github.com/Linerre/f11ad4a6a934dcf01ee8415c9457e7b2#choosing-the-right-init-file
  ZSHENV="$HOME/.zprofile"
else
  ZSHENV="$HOME/.zshenv"
fi

# Dependencies

common_deps=(
  zsh
  figlet
  fzf
  toilet
  ripgrep
  tldr # It's like only the examples sections from manuals, but from a website instead
  htop # for top. Displays the same metrics as top in a pleasant and colorful display
  ncdu # for du
  jq # is like sed or grep but specifically designed to deal with JSON data
  aspell
  stow
)

brew_deps=(
  coreutils
  grep # GNU grep is not part of coreutils
  findutils
  fd # for find: It's find with fewer options, but good defaults for typical uses
  lsd # for ls
  gnutls
)

debian_deps=(
  make
  fd-find
)

link() {
  mkdir -p "$ZSH_CACHE"
  mkdir -p "$ZSH_DATA_HOME"
  mkdir -p "$ZDOTDIR"

  mklink config/{*,.*} "$ZDOTDIR"

  touch $ZDOTDIR/zshrc.local
  touch $ZDOTDIR/zshenv.local
}

install() {
  case $(_os) in
    macos)
      brew install ${common_deps[@]} ${brew_deps[@]}
      ;;
    debian)
      sudo apt update && sudo apt install -y ${common_deps[@]} ${debian_deps[@]}
      ;;
  esac

  echo-note "Please enter your sudo password to set ZSH as the default shell"
  sudo -v

  local zsh="$(command -v zsh)"
  if ! grep -Fxq "$zsh" /etc/shells; then
    echo "$zsh" | sudo tee -a /etc/shells
  fi

  echo-note "Move ZDOTDIR from $HOME to reduce dotfile pollution."
  tee $ZSHENV <<< $'ZDOTDIR="$HOME/.config/zsh"\nsource "$ZDOTDIR/.zshenv"'

  sudo chsh -s $zsh $(whoami)

  echo-info "Restart the terminal if it continues using a different shell."
  exec $zsh -l
  compaudit | xargs chmod g-w
}

update() {
  # Update zpugins and recompile
  if [[ -e ${ZINIT[BIN_DIR]}/zinit.zsh ]]; then
    source ${ZINIT[BIN_DIR]}/zinit.zsh
    zinit self-update
    zinit update --all
    zinit compile --all
  fi

  case $(_os) in
    macos)
      brew upgrade ${common_deps[@]} ${brew_deps[@]}
      ;;
    debian)
      sudo apt update && sudo apt install -y ${common_deps[@]} ${debian_deps[@]}
      ;;
  esac

  compaudit | xargs chmod g-w
}

clean() {
  rm -rfv "$ZSHENV" "$ZDOTDIR" "$ZSH_CACHE"

  # Stop Zsh from displaying messages about missing startup files
  touch "$HOME/.zshrc"
}

init "$@"
