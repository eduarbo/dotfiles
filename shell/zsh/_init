#!/usr/bin/env zsh
source ${0:A:h}/../../deploy
source config/.zshenv

# Dependencies

common_deps=(
  zsh
  fasd
  figlet
  fzf
  toilet
  ripgrep
  tldr # It's like only the examples sections from manuals, but from a website instead
  htop # for top. Displays the same metrics as top in a pleasant and colorful display
  ncdu # for du
  jq # is like sed or grep but specifically designed to deal with JSON data
  aspell
  stow
)

brew_deps=(
  coreutils
  findutils
  zsh-history-substring-search
  fd # for find: It's find with fewer options, but good defaults for typical uses
  lsd # for ls
  gnutls
)

debian_deps=(
  make
  fd-find
)

install() {
  case $(_os) in
    macos)
      brew install ${common_deps[@]} ${brew_deps[@]}

      zshenv=/etc/zshenv
      ;;
    debian)
      sudo apt update && sudo apt install -y ${common_deps[@]} ${debian_deps[@]}

      zshenv=/etc/zsh/zshenv
      ;;
  esac

  local zsh="$(command -v zsh)"

  echo-note "Please enter your sudo password to set ZSH as the default shell"
  sudo -v

  grep $zsh /etc/shells &>/dev/null || sudo tee -a /etc/shells <<<$zsh
  sudo tee $zshenv <<<"export ZDOTDIR=\"${ZDOTDIR/$HOME/\$HOME}\""

  mkdir -p "$XDG_DATA_HOME/zsh"

  sudo chsh -s $zsh $(whoami)
  echo-info "Restart the terminal if it continues using a different shell."
  exec $zsh
}

update() {
  # Update zpugins and recompile
  if [[ -e ${ZINIT[BIN_DIR]}/zinit.zsh ]]; then
    source ${ZINIT[BIN_DIR]}/zinit.zsh
    zinit self-update
    zinit update --all
    zinit compile --all
  fi

  case $(_os) in
    macos)
      brew upgrade ${common_deps[@]} ${brew_deps[@]}
      ;;
    debian)
      sudo apt update && sudo apt install -y ${common_deps[@]} ${debian_deps[@]}
      ;;
  esac
}

link() {
  mkdir -p "$ZSH_CACHE"
  mklink config "$ZDOTDIR"
}

clean() {
  case $(_os) in
    macos)
      _backup --delete "~/.zshenv"
      ;;
  esac

  rm -rfv "$ZDOTDIR" "$ZSH_CACHE"
}

init "$@"
