#!/usr/bin/env zsh
source ${0:A:h}/../../deploy
source config/.zprofile

ZPROFILE="$HOME/.zprofile"
ZSH="$(command -v zsh)"

# Dependencies

common_deps=(
  zsh
  figlet
  fzf
  toilet
  ripgrep
  tldr # It's like only the examples sections from manuals, but from a website instead
  htop # for top. Displays the same metrics as top in a pleasant and colorful display
  ncdu # for du
  jq # is like sed or grep but specifically designed to deal with JSON data
  aspell
  stow
)

brew_deps=(
  coreutils
  grep # GNU grep is not part of coreutils
  findutils
  zsh-history-substring-search
  fd # for find: It's find with fewer options, but good defaults for typical uses
  lsd # for ls
  gnutls
)

debian_deps=(
  make
  fd-find
)

install() {
  echo-info "INSTALLING"
  case $(_os) in
    macos)
      echo-info "brew install"
      brew install ${common_deps[@]} ${brew_deps[@]}
      ;;
    debian)
      sudo apt update && sudo apt install -y ${common_deps[@]} ${debian_deps[@]}
      ;;
  esac

  echo-note "Please enter your sudo password to set ZSH as the default shell"
  sudo -v

  grep $ZSH /etc/shells &>/dev/null || sudo tee -a /etc/shells <<<$ZSH

  echo-note "Move ZDOTDIR from $HOME to reduce dotfile pollution."
  tee $ZPROFILE <<< $'ZDOTDIR="$HOME/.config/zsh"\nsource "$ZDOTDIR/.zprofile"'

  sudo chsh -s $ZSH $(whoami)

  echo-info "Restart the terminal if it continues using a different shell."
  exec "$ZSH -l"
}

update() {
  # Update zpugins and recompile
  if [[ -e ${ZINIT[BIN_DIR]}/zinit.zsh ]]; then
    source ${ZINIT[BIN_DIR]}/zinit.zsh
    zinit self-update
    zinit update --all
    zinit compile --all
  fi

  case $(_os) in
    macos)
      brew upgrade ${common_deps[@]} ${brew_deps[@]}
      ;;
    debian)
      sudo apt update && sudo apt install -y ${common_deps[@]} ${debian_deps[@]}
      ;;
  esac
}

link() {
  echo-info "mkdir -p "$ZSH_CACHE""
  mkdir -p "$ZSH_CACHE"
  echo-info "mkdir -p "$ZSH_DATA_HOME""
  mkdir -p "$ZSH_DATA_HOME"

  echo-info "mklink config "$ZDOTDIR""
  mklink config "$ZDOTDIR"
  echo-info "mklink $HOME/Documents/config/zsh $ZSH_DATA_HOME"
  mklink $HOME/Documents/config/zsh $ZSH_DATA_HOME

  echo-info "touch $ZSH_DATA_HOME/local.zsh"
  touch $ZSH_DATA_HOME/local.zsh
}

clean() {
  echo-info "Remove dirs: $ZPROFILE, $ZDOTDIR, $ZSH_CACHE, $ZSH_DATA_HOME"
  rm -rfv "$ZPROFILE" "$ZDOTDIR" "$ZSH_CACHE" "$ZSH_DATA_HOME"

  # Stop Zsh from displaying messages about missing startup files
  echo-info "touch $HOME/.zshrc"
  touch "$HOME/.zshrc"
}

init "$@"
