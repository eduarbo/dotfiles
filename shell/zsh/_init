#!/usr/bin/env zsh
source ${0:A:h}/../../deploy
source .zshenv

# Dependencies

common_deps=(
  zsh
  fasd
  figlet
  toilet
  ripgrep
  tldr
  aspell
  stow
)

brew_deps=(
  coreutils
  zsh-history-substring-search
  fd
)

debian_deps=(
  make
  fd-find
)

install() {
  case $(_os) in
    macos)
      brew install ${common_deps[@]} ${brew_deps[@]}

      zshenv=/etc/zshenv
      ;;
    debian)
      sudo apt update && sudo apt install -y ${common_deps[@]} ${debian_deps[@]}

      # install fd manually if it's missing from the package manager (it is for Ubuntu 18.04)
      if ! _is_callable fdfind; then
        curl -s https://api.github.com/repos/sharkdp/fd/releases/latest \
          | grep "browser_download_url.*fd_.*_amd64.deb" \
          | cut -d : -f 2,3 \
          | tr -d '"' \
          | wget -O "/tmp/fd_amd64.deb" -qi -
        sudo dpkg -i "/tmp/fd_amd64.deb"
      fi

      zshenv=/etc/zsh/zshenv
      ;;
  esac

  [[ $SHELL == */zsh ]] || sudo chsh -s $(which zsh) $(whoami)

  local zsh="$(command -v zsh)"
  grep $zsh /etc/shells &>/dev/null || sudo tee -a /etc/shells <<<$zsh
  sudo tee $zshenv <<<"export ZDOTDIR=\"${ZDOTDIR/$HOME/\$HOME}\""

  exec $zsh
}

update() {
  # Update zpugins and recompile
  if [[ -e ${ZPLGM[BIN_DIR]}/zplugin.zsh ]]; then
    source ${ZPLGM[BIN_DIR]}/zplugin.zsh
    zplugin self-update
    zplugin update --all
    zplugin compile --all
  fi

  _upgrade
}

link() {
  mkdir -p "$ZSH_CACHE"
  mklink {.zsh*,functions,figlet-fonts} $ZDOTDIR/

  case $(_os) in
    macos)
      mklink .zshenv ~/.zshenv
      ;;
  esac
}

clean() {
  rm -rfv "$ZDOTDIR" "$ZSH_CACHE/*";

  case $(_os) in
    macos)
      rm -f "~/.zshenv"
      ;;
  esac

  # Go back to bash
  sudo chsh -s $(command -v bash) $(whoami)
  exec bash
}

_upgrade() {
  case $(_os) in
    macos)
      brew upgrade ${common_deps[@]} ${brew_deps[@]}
      ;;
    debian)
      sudo apt update && sudo apt install -y ${common_deps[@]} ${debian_deps[@]}
      ;;
  esac
}

init "$@"
